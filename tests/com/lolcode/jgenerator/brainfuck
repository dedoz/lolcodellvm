HAI
I HAS A BOX INSTRUCTIONS
I HAS A IPTR
IPTR R 0
I HAS A BOX LOOPZ
I HAS A BOX LOOPSTACKZ
I HAS A LSPTR
LSPTR R 0

BTW Read in BF instructions, terminated with "!"
VISIBLE "ENTER BRAINFUCK CODE, TERMINATED WITH :'!:':)"
I HAS A CODESTRING
GIMMEH CODESTRING
I HAS A IDX ITZ 0
IM IN YR CODE UPPIN YR IDX TIL BOTH SAEM IDX AN TAKE FROM CODESTRING LABELED "length"
  BTW GIMMEH READVAR
  BTW PUT READVAR IN INSTRUCTIONS N LABEL AS IPTR
  PUT TAKE FROM CODESTRING LABELED IDX IN INSTRUCTIONS N LABEL AS IPTR

  BOTH SAEM TAKE FROM INSTRUCTIONS LABELED IPTR AN "[", ORLY
    YRLY
      PUT IPTR IN LOOPSTACKZ N LABEL AS LSPTR
      LSPTR R SUM OF LSPTR AN 1
  OIC

  BOTH SAEM TAKE FROM INSTRUCTIONS LABELED IPTR AN "]", ORLY
    YRLY
      I HAS A STARTPTR
      LSPTR R DIFF OF LSPTR AN 1
      STARTPTR R TAKE FROM LOOPSTACKZ LABELED LSPTR
      PUT IPTR IN LOOPZ N LABEL AS STARTPTR
      PUT STARTPTR IN LOOPZ N LABEL AS IPTR
  OIC

  BOTH SAEM TAKE FROM INSTRUCTIONS LABELED IPTR AN "!", ORLY
    YRLY
      GTFO
    NOWAI
      IPTR R SUM OF IPTR AN 1
  OIC
IM OUTTA YR CODE
I HAS A BOX LTAPE
I HAS A BOX RTAPE
I HAS A LPTR
LPTR R 0
I HAS A RPTR
RPTR R 0
I HAS A CELL
CELL R 0
IPTR R 0
IM IN YR LOOP
  I HAS A THING
  THING R TAKE FROM INSTRUCTIONS LABELED IPTR
  BOTH SAEM THING AN ">", ORLY
    YRLY
      PUT CELL IN LTAPE N LABEL AS LPTR
      LPTR R SUM OF LPTR AN 1
      BOTH SAEM RPTR AN 0, ORLY
        YRLY
          CELL R 0
        NOWAI
          RPTR R DIFF OF RPTR AN 1
          CELL R TAKE FROM RTAPE LABELED RPTR
      OIC
  OIC
  BOTH SAEM THING AN "<", ORLY
    YRLY
      PUT CELL IN RTAPE N LABEL AS RPTR
      RPTR R SUM OF RPTR AN 1
      BOTH SAEM LPTR AN 0, ORLY
        YRLY
          CELL R 0
        NOWAI
          LPTR R DIFF OF LPTR AN 1
          CELL R TAKE FROM LTAPE LABELED LPTR
      OIC
  OIC
  BOTH SAEM THING AN "+", ORLY
    YRLY
      CELL R SUM OF CELL AN 1
  OIC
  BOTH SAEM THING AN "-", ORLY
    YRLY
      CELL R DIFF OF CELL AN 1
  OIC
  BOTH SAEM THING AN ".", ORLY
    YRLY
      VISIBLE CELL
      VISIBLE " "
  OIC
  BOTH OF BOTH SAEM THING AN "[" AN BOTH SAEM CELL AN 0, ORLY
    YRLY
      IPTR R TAKE FROM LOOPZ LABELED IPTR
  OIC
  BOTH OF BOTH SAEM THING AN "]" AN DIFFRINT CELL AN 0, ORLY
    YRLY
      IPTR R TAKE FROM LOOPZ LABELED IPTR
  OIC
  BOTH SAEM THING AN "!", ORLY
    YRLY
      GTFO
  OIC
  IPTR R SUM OF IPTR AN 1
IM OUTTA YR LOOP
KTHXBYE